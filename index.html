<!DOCTYPE html>
<html>
  <head>
  <title>End Times</title>
  <meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">
  <link rel="stylesheet" href="graphics.css">
  <script src="https://ajax.googleapis.com/ajax/libs/jquery/1.11.3/jquery.min.js"></script>
  
  <script src="./common/webgl-utils.js"></script>
  <script src="./common/initShaders.js"></script>
  <script src="./common/MV.js"></script>

  <script src="./common/require.js"></script>

  <script src="./source/main.js"></script>
  <script src="./source/controls.js"></script>  
  <script src="./source/camera.js"></script>
  <script src="./source/graphics.js"></script>
  <script src="./source/physics.js"></script>
  <script src="./source/ball.js"></script>
  <script src="./source/sky.js"></script>

  <script id="sky.vert" type="text/glsl">
    attribute vec2 vPos;
    uniform mat4 inv_mvp;
    varying vec3 tex_coord;
    void main(){
      gl_Position = vec4(vPos, 1.0, 1.0);
      tex_coord = (inv_mvp * gl_Position).xyz;
    }
  </script>
  <script id="sky.frag" type="text/glsl">
    precision mediump float;
    uniform samplerCube samp;
    varying vec3 tex_coord;
    void main(){
      gl_FragColor = textureCube(samp, tex_coord);
    }
  </script>


  <script id="earth.vert" type="text/glsl">
    attribute vec3 vert;
    attribute vec3 norm;

    uniform mat4 vpMat, modelMat;
    uniform vec3 cameraPos;

    varying vec3 cameraNormal;
    varying vec3 worldNormal;
    varying vec3 localNormal;

    void main () {
      vec3 worldPos = vec3(modelMat * vec4(vert, 1.0));
           
      worldNormal = vec3 (modelMat * vec4(norm, 0.0));
      localNormal = norm;      
      cameraNormal = cameraPos - worldPos;
      
      gl_Position = vpMat * vec4(worldPos, 1.0);
    }
  </script>

  <script id="earth.frag" type="text/glsl">
    precision mediump float;
    //#define CHEAP_CLOUDS

    varying vec3 cameraNormal;
    varying vec3 worldNormal;
    varying vec3 localNormal;
    
    uniform samplerCube samp;
    uniform vec3 lightDir;
    uniform vec3 cloudColor;
    uniform float time;

    mat2 m2 = mat2( 0.970,  0.242, -0.242,  0.970 );
    mat2 noiseFunc(in float a) {
      float c = cos(a), s = sin(a);return mat2(c,-s,s,c);
    }
    float tri(in float x) {
      return abs(fract(x)-.5);
    }
    vec2 triOther(in vec2 inVec) {
      return vec2(tri(inVec.x+tri(inVec.y*2.)),tri(inVec.y+tri(inVec.x*2.)));
    }
    float triangleNoise(in vec2 inVec)
    {
      float zed = 1.5, z2 = 1.5, rz = 0.;
      vec2 brep = inVec;
      for (float i = 0.; i <= 3.; ++i) {
        vec2 deg = triOther(brep * 2.) * .8;
        deg *= noiseFunc(time * .3);
        inVec += deg / z2;
        brep *= 1.6;
        z2 *= .6;
        zed *= 1.8;
        inVec *= 1.2;
        inVec*= m2;        
        rz+= (tri(inVec.x + tri(inVec.y))) / zed;
      }
      return rz;
    }

    void main () {

      vec3 specColor = vec3(1.0, 0.9, 0.7);
      vec3 nSurface = normalize(worldNormal);
      vec3 nModel = normalize(localNormal);
      vec3 nCamera = normalize(cameraNormal);

      vec3 diffuseColor = textureCube(samp, nModel).xyz;

      float cloudFactor = 0.1 -
        (triangleNoise(vec2((tan(nModel.y) - cos(nModel.z + 0.8)), nModel.y)) * (1.5 + sin(time * 0.1))) *
        (triangleNoise(vec2((sin(nModel.x + 0.4) - tan(nModel.y)), nModel.y)) * (1.5 - sin(time * 0.1))) *
        triangleNoise(vec2((pow(cos(nModel.x), 2.0) - pow(sin(nModel.z + 0.2), 2.0)) * 1.9, nModel.y * 0.7));

      #ifndef CHEAP_CLOUDS
      float angle = atan(nModel.z, nModel.x);
      float angle2 = atan(nModel.x, nModel.z);
      float angMask = abs(sin(angle));
      float angMask2 = abs(sin(angle2));
      float cloudFactor2 = 0.32 - (
        triangleNoise(vec2(angle, nModel.y * 3.)) * (1.35 - abs(nModel.y)) * angMask * angMask +
        triangleNoise(vec2(angle2, nModel.y * 3.)) * (1.35 - abs(nModel.y)) * angMask2 * angMask2);
      cloudFactor -= cloudFactor2 * 0.08;
      cloudFactor = max((cloudFactor - 0.075) * 32., 0.); // increase cloud contrast
      #else
      cloudFactor = max((cloudFactor - 0.091) * 70., 0.); // increase cloud contrast
      #endif

      specColor *= clamp((diffuseColor.b - diffuseColor.r) - cloudFactor, 0., 0.1) * 3. *
    		pow(
    			max(
    				dot(
    					reflect(-lightDir, nSurface),
    				nCamera),
    			0.),
    		16.);

      vec3 cloudValue = (cloudColor * cloudFactor);
      diffuseColor += cloudValue;
      float normDotLit = dot(nSurface, lightDir);
      diffuseColor *= clamp(normDotLit + cloudFactor * 0.3, 0.02, 1.0);
      diffuseColor += max(vec3(0.2, 0.5, 1.0) * normDotLit * 0.3 - 0.05, 0.0);

      gl_FragColor = vec4(diffuseColor + specColor, 1.0);     
    }
  </script>


  <script id="atmosphere.vert" type="text/glsl">
    attribute vec3 vert;
    attribute vec3 norm;

    uniform mat4 vpMat, modelMat;
    uniform vec3 cameraPos;

    varying vec3 cameraNormal;
    varying vec3 worldNormal;

    void main () {
      vec3 worldPos = vec3(modelMat * vec4(vert * 1.4, 1.0));
           
      worldNormal = vec3 (modelMat * vec4(norm, 0.0));        
      cameraNormal = cameraPos - worldPos;
      
      gl_Position = vpMat * vec4(worldPos, 1.0);
    }
  </script>

  <script id="atmosphere.frag" type="text/glsl">
    precision mediump float;

    varying vec3 cameraNormal;
    varying vec3 worldNormal;
    
    uniform vec3 lightDir;

    void main () {

      vec3 nSurface = normalize(worldNormal);
      vec3 nCamera = normalize(cameraNormal);

		  float normDotCam = dot(nSurface, nCamera);

      float diffuseAlpha =
        (
          (1.0 - ((normDotCam + 1999.0) * 0.0005)) - //0.456
          pow((1.0 - normDotCam), 8.0) //0.177
        ) * 1200.0;

      diffuseAlpha *= pow(max(1.1 - abs(normDotCam - 0.69),0.4), 4.0);

      diffuseAlpha *=
        max(
          dot(
            reflect(-lightDir, nSurface),
          nCamera) + 0.5,
        0.) +
        max(
          0.4 - dot(-lightDir, nSurface),
        0.) * 0.3; 

      gl_FragColor = vec4(0.2, 0.5, 1.0, diffuseAlpha);
    }
  </script>

  
  </head>

  <body>
    <canvas id="gl-canvas" width="800" height="600">
      Your browser doesn't support the HTML5 canvas element.
    </canvas>
    <!-- <div class="instrBox">
      <p>
      Click-drag to rotate view. Scroll to zoom. Or use arrow keys to pan/zoom. Spacebar pauses/unpauses the animation. Use [ and ] to decrease or increase animation speed. Credits for Earth image data go to <a href="http://www.humus.name/index.php?page=Textures">Humus</a>
      </p>
    </div> -->
  </body>

</html>
